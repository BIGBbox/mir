[@锻造]
{
#IF
#ACT
MOV S$锻造主页 1
MOV S$锻造子页 1
MOV S$锻造List 1
GOTO @锻造选择

[@锻造主选]
#IF
#ACT
MOV S$锻造子页 1
MOV S$锻造List 1
GOTO @锻造选择

[@锻造子选择]
#IF
#ACT
MOV S$锻造List 1
GOTO @锻造选择


[@锻造选择]
#IF
#ACT
MOV S$主签图1 1900000663
MOV S$主签图2 1900000663
MOV S$主签图3 1900000663
MOV S$主签图4 1900000663
MOV S$主签图5 1900000663
MOV S$主签图6 1900000663
MOV S$主签图7 1900000663
MOV S$主签图8 1900000663
MOV S$主签图9 1900000663
MOV S$主签图<$STR(S$锻造主页)> 1900000662

MOV N$读取行 1
MOV N$主页签Y 110
GetListStringEx ..\QuestDiary\表格文件\锻造系统\标题.txt <$STR(N$读取行)> S$锻造读取<$STR(N$读取行)>_ #
MOV S$锻造显示 <Button|x=215.0|y=110.0|color=255|size=18|nimg=public/<$STR(S$主签图<$STR(N$读取行)>)>.png|text=<$STR(S$锻造读取<$STR(N$读取行)>_0)>|link=@锻造主选#锻造主页=1>

INC N$读取行 1
INC N$主页签Y 40
GetListStringEx ..\QuestDiary\表格文件\锻造系统\标题.txt <$STR(N$读取行)> S$锻造读取<$STR(N$读取行)>_ #
INC S$锻造显示 <Button|x=215.0|y=<$STR(N$主页签Y)>|color=255|size=18|nimg=public/<$STR(S$主签图<$STR(N$读取行)>)>.png|text=<$STR(S$锻造读取<$STR(N$读取行)>_0)>|link=@锻造主选#锻造主页=2>

INC N$读取行 1
INC N$主页签Y 40
GetListStringEx ..\QuestDiary\表格文件\锻造系统\标题.txt <$STR(N$读取行)> S$锻造读取<$STR(N$读取行)>_ #
INC S$锻造显示 <Button|x=215.0|y=<$STR(N$主页签Y)>|color=255|size=18|nimg=public/<$STR(S$主签图<$STR(N$读取行)>)>.png|text=<$STR(S$锻造读取<$STR(N$读取行)>_0)>|link=@锻造主选#锻造主页=3>

INC N$读取行 1
INC N$主页签Y 40
GetListStringEx ..\QuestDiary\表格文件\锻造系统\标题.txt <$STR(N$读取行)> S$锻造读取<$STR(N$读取行)>_ #
INC S$锻造显示 <Button|x=215.0|y=<$STR(N$主页签Y)>|color=255|size=18|nimg=public/<$STR(S$主签图<$STR(N$读取行)>)>.png|text=<$STR(S$锻造读取<$STR(N$读取行)>_0)>|link=@锻造主选#锻造主页=4>

INC N$读取行 1
INC N$主页签Y 40
GetListStringEx ..\QuestDiary\表格文件\锻造系统\标题.txt <$STR(N$读取行)> S$锻造读取<$STR(N$读取行)>_ #
INC S$锻造显示 <Button|x=215.0|y=<$STR(N$主页签Y)>|color=255|size=18|nimg=public/<$STR(S$主签图<$STR(N$读取行)>)>.png|text=<$STR(S$锻造读取<$STR(N$读取行)>_0)>|link=@锻造主选#锻造主页=5>

INC N$读取行 1
INC N$主页签Y 40
GetListStringEx ..\QuestDiary\表格文件\锻造系统\标题.txt <$STR(N$读取行)> S$锻造读取<$STR(N$读取行)>_ #
INC S$锻造显示 <Button|x=215.0|y=<$STR(N$主页签Y)>|color=255|size=18|nimg=public/<$STR(S$主签图<$STR(N$读取行)>)>.png|text=<$STR(S$锻造读取<$STR(N$读取行)>_0)>|link=@锻造主选#锻造主页=6>

MOV S$锻造子字符串 <$STR(S$锻造读取<$STR(S$锻造主页)>_1)>
MOV N$子对比 1
MOV N$子页签Y 110
MOV S$子LIST串
GOTO @子标题展现


#IF
#SAY

<Img|x=184.0|y=40.0|esc=1|show=0|bg=1|move=0|img=public/1900000681.png|reset=1>
<Layout|x=937.0|y=30.0|width=80|height=80|link=@exit>
<Text|x=264.0|y=56.0|color=95|size=20|outlinecolor=0|outline=1|text=锻造>
<Button|x=962.0|y=50.0|nimg=public/1900000510.png|pimg=public/1900000511.png|link=@exit>
<Img|x=332.0|y=92.0|width=2|height=444|esc=0|img=public/bg_yyxsz_02.png>
<Img|x=513.0|y=92.0|width=2|height=444|esc=0|img=public/bg_yyxsz_02.png>
<Img|x=700.0|y=325.0|img=public/1900000651_3.png>
<Img|x=172.0|y=41.0|img=public/1900000610_1.png|esc=0>
<ListView|children={<$STR(S$子LIST串)>}|x=337.0|y=110|width=180|height=420|direction=1>
<$STR(S$<COLOR=#109429  锻造显示>)>
<Button|x=682.0|y=470.0|color=255|mimg=public/1900000660.png|size=18|pimg=public/1900000660.png|nimg=public/1900000660.png|text=合成|link=@开始锻造>


[@子标题展现]
#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
#ACT
MOV S$子选图标 1900000663_1
INC S$锻造显示 <Img|id=子箭头<$STR(N$子对比)>|x=3.0|y=4.0|img=public/btn_szjm_01_6.png|esc=0>
#ELSEACT
MOV S$子选图标 1900000663_2
INC S$锻造显示 <Img|id=子箭头<$STR(N$子对比)>|x=30.0|y=2.0|rotate=90|esc=0|img=public/btn_szjm_01_6.png>

#IF
#ACT
GetValidstr <$STR(S$锻造子字符串)> & S$锻造物品串 S$锻造子字符串
GetValidstr <$STR(S$锻造物品串)> * S$锻造子按钮 S$锻造物品串
INC S$锻造显示 <Button|id=子按钮<$STR(N$子对比)>|children={子箭头<$STR(N$子对比)>}|x=0|y=<$STR(N$子页签Y)>|nimg=public/<$STR(S$子选图标)>.png|color=255|size=18|text=<$STR(S$锻造子按钮)>|link=@锻造子选择#锻造子页=<$STR(N$子对比)>>
INC S$子LIST串 子按钮<$STR(N$子对比)>,

MOV S$锻造材料串
MOV N$锻造货币 0
MOV N$锻造机率 0

#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
#ACT
MOV N$锻造ListNum 1
GOTO @锻造物品签

#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
#ACT
GetDBItemFieldValue <$STR(S$锻造目标物品)> idx N$锻造Idx
INC S$锻造显示 <ItemShow|x=702.0|y=326.0|width=70|height=70|itemid=<$STR(N$锻造Idx)>|itemcount=1|showtips=1|bgtype=0>
GetListString ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt 0 S$锻造材料串
GetListString ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt 1 S$锻造货币 N$锻造货币
GetListString ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt 2 S$锻造机率 N$锻造机率
MOV N$锻造材料L 0
MOV S$锻造长度计算 <$STR(S$锻造材料串)>
GOTO @解析材料长度

#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
LARGE N$锻造货币 0
#ACT
MOV N$偏移初始量 9
MOV N$额度串坐标 708
GOTO @锻造价格串偏移


#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
LARGE N$锻造货币 0
#ACT
INC S$锻造显示 <RText|x=<$STR(N$额度串坐标)>|y=410.0|color=255|size=16|outlinecolor=0|outline=1|text=<需要消耗：/FCOLOR=249><<$STR(N$锻造货币)><$STR(S$锻造货币)>/FCOLOR=250>>


#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
LARGE N$锻造机率 0
#ACT
INC S$锻造显示 <RText|x=675|y=435.0|color=255|size=16|outlinecolor=0|outline=1|text=<成功机率：/FCOLOR=249><<$STR(N$锻造机率)>%/FCOLOR=250>>


#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
EQUAL N$锻造材料L 1
#ACT
MOV N$锻造材料X 700
MOV N$锻造材料INC 0
INC S$锻造显示 <Img|x=731.0|y=232.0|img=public/haha/bg_clhczy_01.png|esc=0>


#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
EQUAL N$锻造材料L 2
#ACT
MOV N$锻造材料X 580
MOV N$锻造材料INC 240
INC S$锻造显示 <Img|x=646.0|y=232.0|img=public/haha/bg_clhczy_02.png|esc=0>

#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
EQUAL N$锻造材料L 3
#ACT
MOV N$锻造材料X 580
MOV N$锻造材料INC 120

#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
EQUAL N$锻造材料L 4
#ACT
MOV N$锻造材料X 580
MOV N$锻造材料INC 80

#IF
EQUAL <$STR(N$子对比)> <$STR(S$锻造子页)>
EQUAL N$锻造材料L 5
#ACT
MOV N$锻造材料X 540
MOV N$锻造材料INC 80


#IF 
NOT EQUAL S$锻造材料串
#ACT
GOTO @解析材料串


#IF
#ACT
INC N$子对比 1

#IF
NOT EQUAL S$锻造子字符串
#ACT
GOTO @子标题展现

[@锻造价格串偏移]
#IF
#ACT
MOV S$临时 需要消耗：<$STR(N$锻造货币)><$STR(S$锻造货币)>
#IF
CheckStringlength <$STR(S$临时)> > <$STR(N$偏移初始量)>
#ACT
INC N$偏移初始量 1
DEC N$额度串坐标 5
GOTO @锻造价格串偏移


[@解析材料串]
#IF 
NOT EQUAL S$锻造材料串
#ACT
GetValidstr <$STR(S$锻造材料串)> # S$取出材料串 S$锻造材料串
INC S$锻造显示 <Img|x=<$STR(N$锻造材料X)>|y=162.0|img=public/1900000651_3.png>
GetValidstr <$STR(S$取出材料串)> = S$取出材料key S$取出材料count
GetDBItemFieldValue <$STR(S$取出材料key)> idx N$取出Idx
INC S$锻造显示 <ItemShow|x=<$STR(N$锻造材料X)>|y=162.0|width=70|height=70|itemid=<$STR(N$取出Idx)>|itemcount=1|showtips=1|bgtype=0>
MOV N$锻造数量坐标 <$STR(N$锻造材料X)>
INC N$锻造数量坐标 40
GETBAGITEMCOUNT <$STR(S$取出材料key)> N$锻造当前拥有
MOV N$锻造当前颜色 249
#ELSEACT
BREAK

#IF
NOT SMALL N$锻造当前拥有 <$STR(S$取出材料count)>
#ACT
MOV N$锻造当前颜色 251

#IF
#ACT
INC S$锻造显示 <RText|x=<$STR(N$锻造数量坐标)>|y=202.0|color=255|size=16|outlinecolor=0|outline=1|text=<<$STR(N$锻造当前拥有)>/FCOLOR=<$STR(N$锻造当前颜色)>></<$STR(S$取出材料count)>/FCOLOR=250>>
INC N$锻造材料X <$STR(N$锻造材料INC)>
GOTO @解析材料串

[@解析材料长度]
#IF 
NOT EQUAL S$锻造长度计算
#ACT
GetValidstr <$STR(S$锻造长度计算)> # S$长度取出 S$锻造长度计算
INC N$锻造材料L 1
GOTO @解析材料长度


[@锻造物品签]
#IF
EQUAL N$锻造ListNum <$STR(S$锻造List)>
#ACT
MOV S$锻造ListIcon bg_hhzy_01_1
#ELSEACT
MOV S$锻造ListIcon bg_hhzy_01_3

#IF 
NOT EQUAL S$锻造物品串
#ACT
GetValidstr <$STR(S$锻造物品串)> * S$取出物品 S$锻造物品串
INC S$锻造显示 <Button|id=子子<$STR(N$锻造ListNum)>|x=0|y=<$STR(N$子页签Y)>|width=170|height=34|color=255|nimg=public/<$STR(S$锻造ListIcon)>.png|size=16|text=<$STR(S$取出物品)>|link=@锻造选择#锻造List=<$STR(N$锻造ListNum)>>
INC S$子LIST串 子子<$STR(N$锻造ListNum)>,

#IF
EQUAL N$锻造ListNum <$STR(S$锻造List)>
#ACT
MOV S$锻造目标物品 <$STR(S$取出物品)>

#IF 
NOT EQUAL S$锻造物品串
#ACT
INC N$锻造ListNum 1
GOTO @锻造物品签

[@开始锻造]
#IF
NOT CheckContainsTextList ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt :
#ACT
SENDMSG 5 不存在
BREAK

#IF
NOT CHECKBAGSIZE 3
#ACT
SENDMSG 5 锻造至少需要背包预留3格空位。
BREAK
#IF
NOT EQUAL S$滴答 <$UTCNOW8>
#ACT
MOV S$滴答 <$UTCNOW8>
;SENDMSG 5 锻造【<$STR(S$锻造目标物品)>】
GetListString ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt 0 S$锻造材料串
MOV S$检查串 <$STR(S$锻造材料串)>
MOV S$删除串 <$STR(S$锻造材料串)>
GetListString ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt 1 S$锻造货币 N$锻造货币
GetListString ..\QuestDiary\表格文件\锻造系统\合成公式\<$STR(S$锻造目标物品)>.txt 2 S$锻造机率 N$锻造机率
MOV N$合成<$STR(S$锻造目标物品)> 1
GOTO @锻造检查
#ELSEACT
SENDMSG 5 超速操作。
BREAK
;-----------------------------------检验逃避逻辑
#IF
EQUAL N$合成<$STR(S$锻造目标物品)> 1
#ACT
BREAK
;-----------------------------------检验结束
#IF
CHECKMONEY <$STR(S$锻造货币)> < <$STR(N$锻造货币)>
#ACT
SENDMSG 5 您的<$STR(S$锻造货币)>不足<$STR(N$锻造货币)>！
BREAK
#ELSEACT
MONEY <$STR(S$锻造货币)> - <$STR(N$锻造货币)>
#IF
#ACT
MOV N$扣除<$STR(S$锻造目标物品)> 1
GOTO @锻造扣除
MOVR N$生成机率 100
;-----------------------------------检验逃避逻辑
#IF
EQUAL N$扣除<$STR(S$锻造目标物品)> 1
#ACT
BREAK
;-----------------------------------检验结束
#IF
SMALL <$STR(N$生成机率)> <$STR(N$锻造机率)>
#ACT
GIVE <$STR(S$锻造目标物品)> 1
SENDMSG 6 恭喜你通过锻造成功获得：<$STR(S$锻造目标物品)>x1
GOTO @锻造选择
BREAK
#ELSEACT
SENDMSG 6 很遗憾，本次锻造失败了！
BREAK


[@锻造检查]
#IF
NOT EQUAL S$检查串
#ACT
GetValidstr <$STR(S$检查串)> # S$取出材料串 S$检查串
GetValidstr <$STR(S$取出材料串)> = S$取出材料key S$取出材料count
#IF
NOT CHECKITEM <$STR(S$取出材料key)> <$STR(S$取出材料count)>
#ACT
SENDMSG 5 您身上缺少合成材料：<$STR(S$取出材料key)>x<$STR(S$取出材料count)>
BREAK
#IF
NOT EQUAL S$检查串
#ACT
GOTO @锻造检查
#ELSEACT
MOV N$合成<$STR(S$锻造目标物品)> 0

[@锻造扣除]
#IF
NOT EQUAL S$删除串
#ACT
GetValidstr <$STR(S$删除串)> # S$取出材料串 S$删除串
GetValidstr <$STR(S$取出材料串)> = S$取出材料key S$取出材料count
#IF
CHECKITEM <$STR(S$取出材料key)> <$STR(S$取出材料count)>
#ACT
TAKE <$STR(S$取出材料key)> <$STR(S$取出材料count)>
#ELSEACT
SENDMSG 5 您身上缺少合成材料：<$STR(S$取出材料key)>x<$STR(S$取出材料count)>
BREAK
#IF
NOT EQUAL S$删除串
#ACT
GOTO @锻造扣除
#ELSEACT
MOV N$扣除<$STR(S$锻造目标物品)> 0

}